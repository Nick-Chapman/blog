
# Quarter: A Minimalist Forth

### Why?

In the [previous article](2.bootstrap.md) we saw how we might bootstrap a Forth system using a minimalistic language where computation was directed by individual characters, instead of blank-delimited words. Quarter is that language.
This article gives examples of coding in Quarter, highlights how it is similar to Forth, and how it differs.

Initially the spartan nature of Quarter might seem overly cryptic, but it is worth remembering that we don't choose to be cryptic just for the sake of it. Our purpose is to allow a full Forth system to be bootstrapped in an Asm-independent way. We shall only write as much Quarter code as necessary to get us to a working Forth system.
The current definition of Quarter has 36 primitives. These are just the primitives which are needed to write a Forth text interpreter.
Not all primitives defined in a Forth kernel need be available to Quarter.

Quarter primitives are accessed via a flat dispatch table, indexed by an Ascii character. This allows a maximum of 128 primitives; although it is probably wise to restrict ourselves to just the printable characters 33 (`!`) to 126 (`~`).

In practice, we use just uppercase and symbolic characters for the kernel primitives, leaving lowercase for use by user definitions.
The names are chosen to be as suggestive as possible to their Forth counterparts,
although this is difficult when everything is named by just a single character.

We increase readability by defining space and newline as no-ops.


### First example: square

In many introductions to Forth, an early motivating example is the user definition for the word which squares a number. Let's do that in Quarter.
As in Forth, `:` and `;` are used to make a new definition.
Given primitives to _dup_ (`D`) the top stack element, and _multiply_ (`*`) the top two values on the stack,
perhaps the following is how we define square in Quarter?

```
:s D* ; ( nope )
```

`:s` does create a new entry in the Quarter dispatch table for a definition under construction, and `;` does terminates the code with whatever is required to _return_ to the caller. But `D*` doesn't work quite as we need.

`D*` does call _dup_ then _multiply_, but these are executed _right now_ as the definition is being created. What we want is to generate code which sequences the calls to `D` and `*` when a call is made to `s`.
To do this in quarter, we can write the definition like this:

```
:s ^D?> ^*?> ;
```

This makes use of three important primitives: _get_, _dispatch_ and _compileComma_

- `^` _Get_ : get the next character from the input stream
- `?` _Dispatch_ : lookup the execution-token for a character
- `>` CompileComma : compile a call to an execution token into the definition being constructed.

`^` and `>` are the standard Forth words `get` and `compile,` exposed as quarter primitives. `?`, which is pronounced _dispatch_, is the quarter level version of `find`. It performs lookup in the flat dispatch table of quarter primitives.

Running the code quad `^D?>` has three steps:

- `^` reads the following `D` from the input stream, leaving the character on the top of the stack.
- `?` looks up the `D` on the stack-top, replacing it with the execution top for _dup_
- `>` compiles a call to _dup_ into the definition being constructed.

The code quad `^*?>` is executed similarly, but for the multiplication primitive.

Constructing definitions in Quarter is like being in Forth but
being forced to switch to _interpreter-mode_ using `[` immediately following the definition start.
If we did this in Forth, the definition of _square_ might look like this:

```
: square [ word dup find compile, word * find compile, ] ;
```

### Bootstrapping

Much of the tedium of writing these _get-X-dispatch-compileComma,_ code quads can be alleviated if we bundle up the three stages into their own definition, which we name `~`.

```
:~ ^^?> ^??> ^>?> ;
```

Now we can simplify the definition for our squaring example to be:
```
:s ~D~* ;
```

The definition of `~` makes writing Quarter code much more palatable.
And we can make further definitions for all the tools we need to write more complicated functions.

```
:' ^^?> ^??> ;
:# 'L ~L, ~> ~, ;
:i ~L ^B?, ~>~H~@ 0# ~, ;
:t ~D~H~@~W~-~W~! ;
```

The above four definitions give us _tick_, _literal_, _if_ and _then_
making use of various Quarter primitives:

- `L` lit
- `,` comma
- `B` 0branch
- `H` here-pointer
- `@` fetch
- `0` zero
- `W` swap
- `-` minus
- `!` store

With some Forth experience, it is easy to guess what these primitives do.
For details see [here](https://github.com/Nick-Chapman/quarter-forth/blob/main/x86/kernel.asm).
How exactly they are being composed takes a little more study.

### Comments

We can define support for comments in Quarter, just as we did for Forth in the [first article in this series](1.rabbit.md) using the following line of squiggles:

```
H@^(`0`E :( ~^ ^)# ~= i ~X t '(#~J ;
```

This defines comment support both in Quarter, via the definition `:(`, but it also constructs an entry in the Forth dictionary with ``H@^(`0`E`` so comments will be available in Forth once we enter the bootstrap interpreter being defined.


### Second example: type

Now we have the basic tools for programming available, lets see a real example.

```
:p ~D~C~D i ~.~1~+ jp t ~P~P ;
```

This code defines `p` to print a string. What would be called `type` in Forth.
It prints each character of a string until the null terminator is reached.
Quarter Forth uses null-terminated strings, as opposed to the counted strings of a more traditional Forth system.
The loop structure is setup by the tail-recursive call `jp`.
The definition uses primitives _dup_ (`D`), _char-fetch_ (`C`) , _emit_ (`.`), drop (`P`), _one_ (`1`) and _plus_ (`+`).

The above definition of `p` above corresponds very directly to how a Forth definition might look:

```
: type ( a -- )
dup c@ dup if emit 1 + recurse
then drop drop ;
```

And this highlights the main difference between Forth and Quarter:

When we write Forth definitions, we are in compile-mode by default, returning to interpret-mode only when explicitly directed by `[`.
To have words like `if` and `then` execute whilst compiling, we mark them as special immediate words.

In contrast in Quarter, everything is executed _right now_ whilst compiling.
It's just that a common thing we do _right now_ is to compile code to be run in the future, by using `~`.


### Third example: interpreter

As a final example, lets see a Forth interpreter written in quarter.
```
h ^]`0` h^[`0`E :[# ~w~W~O~q i ~P~X t ~D~f~D i ~W~P~V j[ t ~P~p ^?#~.~M~A j[;
```

I'll be honest. This isn't easy to parse.
But now we know the basics, we can see the structure emerge.
We see runs of regular code, i.e. `~D~f~D` separated by control flow constructs `i` and `t`.
Branches either _fall through_ to the next branch, or end with exit (`~X`) or recurse (`~j[`)

This interpreter does not handle numeric literals, but only words which are looked up in the Forth dictionary. The core functionality for reading a blank-delimited word and dictionary lookup is defined in sub-definitions for `w` and `f`. But we can see the overall structure of the interpreter here.

Let's break into 7 lines to allow reference:

```
h ^]`0`
h ^[`0`E :[
# ~w~W~O~q
i ~P~X
t ~D~f~D
i ~W~P~V j[
t ~P~p ^?#~.~M~A j[;
```

- Line 1: Define a literal string for `"]"`, used by comparison on line 3.
- Line 2: Forth dictionary header for `[` and quarter definition (`:[`).
- Line 3: Embed literal from line 1 with `#`. Get next word. Test equality against `"]"`?
- Line 4: If equal, then stop recursion.
- Line 5: If not, then look-up in the dictionary.
- Line 6: Found in dictionary: execute (`~V`) and recurse.
- Line 7: Not in dictionary: display error and recurse.


You might compare this Quarter definition of a Forth interpreter with
the Forth definition given in the [previous article](2.bootstrap.md).


### Answers

To summarize, lets answer the questions posed at the end of the [previous article](2.bootstrap.md).

_What primitives are provided by Quarter?_
The short answer is whatever we need, and no more, to implement a Forth Interpreter and compiler.
Currently, there are 36 primitives which can be seen listed
[here](https://github.com/Nick-Chapman/quarter-forth/blob/main/x86/kernel.asm)
in my x86 implementation.
And also [here](https://github.com/Nick-Chapman/tc-quarter/blob/main/src/Execution.hs)
in my Haskell port/emulation.
In the next article we shall see why there is a Haskell version of Quarter Forth.

_How does Quarter differ from conventional Forth?_
The most obvious difference is that programs are composed from single character primitives instead of words.
This might be a significant difficulty if we planned to write very much Quarter code, but since we shall only write what we need to bootstrap Forth, it is not such a big obstacle.
The less obvious difference is that we don't have the modal state of Forth.
There is no compile-mode. We are always in interpret-mode.
This makes our code more verbose when compiling definitions; or to give a positive spin: the code is more explicit.

_What does Quarter code look like?_
We've looked at a few examples in this article.
For more, see the
[full implementation](https://github.com/Nick-Chapman/quarter-forth/blob/main/f/quarter.q)
of the bootstrap interpreter and compiler for Quarter Forth. It runs to just 32 lines, including comments and whitespace, and currently makes up 100% of all Quarter Code in existence!


### Next time

In the [next article](4.typing.md)
we'll explore how we might type-check Forth code.
